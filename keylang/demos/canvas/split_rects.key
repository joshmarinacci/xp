@board("canvas")

//screen and system and the colors are already declared and in scope

var DIRECTIONS
var rects
fun do_splits(rect,depth) {
    if depth < 0 return List(rect)
    var dir = choose(DIRECTIONS)
    var parts = rect.split(dir,randf(0.2,0.8))
    return List(do_splits(parts.get(0),depth-1), do_splits(parts.get(1),depth-1))
}

fun sine(v) { return Math.sin(v)  }
fun sine1(v) { return remap(Math.sin(v), -1, 1, 0,1) }
fun mirror_t(t) {
    if t < 0.5 return t/2
    return 1-t
}

//this function is used by the others
fun drawRect(rect, time) {
    var t = sine1(time * (0.5 + rect.frequency) + rect.phase)
    t = mirror_t(t)
    screen.fillRect(rect, HSL(rect.hue,
        lerp(t,0.2,0.8),
        lerp(t,0.2,1.0))
    )
    screen.strokeRect(rect, BLACK)
}

//this will be called once at app start
@type("start",setup)
fun setup() {
    DIRECTIONS = List("h","v")
    rects = do_splits(screen,4).flatten()
    rects.every((r)=>{
        r.hue = randf(0,1)
        r.phase = randf(0,1)
        r.frequency = randf(0.2,1.5)
    })
}

// this will be called every 0.3 seconds
@type("loop",loop, 0.5)
fun loop() {
    rects.every((rect) => { drawRect(rect,system.time) })
}

